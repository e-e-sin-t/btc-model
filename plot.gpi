#!/usr/local/bin/gnuplot

set datafile separator "," 
set timefmt "%Y-%m-%d"
set decimal locale
set terminal png size 960,480
set key noautotitle
set key spacing 1.5

line_color = "with lines linecolor "
black_line = line_color."'black'"
gray_line = line_color."'gray'"
thin_blue_line = line_color."'blue'"
light_blue_line = line_color."'light-blue'"
blue_line = thin_blue_line." linewidth 2"

# p vs d

min_x = "2010-01-01"; max_x = "2026-01-01"

set xdata time
set format x "%Y"
set format y "$%'g"
set logscale y
set xlabel "date"
set ylabel "price"

set output "plot/p vs d.png"
plot [min_x:max_x] "data.csv" using (column("d")):(column("p")) @black_line, \
     "data.csv" using (column("d")):(column("p_s")) @black_line, \
     "data.csv" using (column("d")):(column("p_r")) @black_line, \
     "data.csv" using (column("d")):(column("p_m")) @blue_line

# p_ixic vs d

min_x = "2010-01-01"; max_x = "2023-06-01"

set yrange [1500:25000]

set output "plot/p_ixic vs d.png"
plot [min_x:max_x] "data.csv" using (column("d")):(column("p_ixic")) @black_line, \
     "data.csv" using (column("d")):(column("p_ixic_s")) @black_line, \
     "data.csv" using (column("d")):(column("p_ixic_r")) @black_line

unset yrange

# p vs d [mirror]

min_x = "2010-01-01"; max_x = "2034-01-01"

set output "plot/p vs d [mirror].png"
plot [min_x:max_x] "data.csv" using (column("d")):(column("p")) @black_line, \
     "data.csv" using (column("d")):(column("p_s")) @black_line, \
     "data.csv" using (column("d")):(column("p_r")) @black_line, \
     "data.csv" using (column("d")):(column("p_m")) @blue_line

# p vs d [2010]

min_x = "2010-10-01"; max_x = "2011-01-01"

set format x "%Y-%m"
set yrange [0.01:1]
set grid

set output "plot/p vs d [2010].png"
plot [min_x:max_x] "data.csv" using (column("d")):(column("p")) @black_line, \
     "data.csv" using (column("d")):(column("p_s")) @black_line, \
     "data.csv" using (column("d")):(column("p_r")) @black_line, \
     "data.csv" using (column("d")):(column("p_m")) @thin_blue_line

# p vs d [2020-2023]

min_x = "2020-06-01"; max_x = "2023-06-01"

set yrange [5000:70000]
unset logscale y

set output "plot/p vs d [2020-2023].png"
plot [min_x:max_x] "data.csv" using (column("d")):(column("p")) @black_line, \
     "data.csv" using (column("d")):(column("p_s")) @black_line, \
     "data.csv" using (column("d")):(column("p_r")) @black_line, \
     "data.csv" using (column("d")):(column("p_m")) @thin_blue_line

unset xdata
unset format
unset yrange
unset grid

# ln(p) vs t

min_x = 0; max_x = 15.25

set output "plot/ln(p) vs t.png"
set xlabel "t"
set ylabel "ln(p)"
plot [min_x:max_x] "data.csv" using (column("t")):(column("ln(p)")) @black_line, \
     "data.csv" using (column("t")):(column("ln(p_s)")) @black_line, \
     "data.csv" using (column("t")):(column("ln(p_r)")) @black_line, \
     "data.csv" using (column("t")):(column("ln(p_m)")) @blue_line

# ln(p) vs ln(t)

min_x = 0; max_x = 8.87

set output "plot/ln(p) vs ln(t).png"
set xlabel "ln(t)"
set ylabel "ln(p)"
plot [min_x:max_x] "data.csv" using (column("ln(t)")):(column("ln(p)")) @black_line, \
     "data.csv" using (column("ln(t)")):(column("ln(p_s)")) @black_line, \
     "data.csv" using (column("ln(t)")):(column("ln(p_r)")) @black_line, \
     "data.csv" using (column("ln(t)")):(column("ln(p_m)")) @blue_line

# ln(ln(p)) vs ln(t)

set output "plot/ln(ln(p)) vs ln(t).png"
set xlabel "ln(t)"
set ylabel "ln(ln(p))"
plot [min_x:max_x] "data.csv" using (column("ln(t)")):(column("ln(ln(p))")) @black_line, \
     "data.csv" using (column("ln(t)")):(column("ln(ln(p_s))")) @black_line, \
     "data.csv" using (column("ln(t)")):(column("ln(ln(p_r))")) @black_line, \
     "data.csv" using (column("ln(t)")):(column("ln(ln(p_m))")) @blue_line

# asin(ln(ln(p))) vs ln(t)

set output "plot/asin(ln(ln(p))) vs ln(t).png"
set xlabel "ln(t)"
set ylabel "asin(ln(ln(p)))"
plot [min_x:max_x] "data.csv" using (column("ln(t)")):(column("asin(ln(ln(p)))")) @black_line, \
     "data.csv" using (column("ln(t)")):(column("asin(ln(ln(p_s)))")) @black_line, \
     "data.csv" using (column("ln(t)")):(column("asin(ln(ln(p_r)))")) @black_line, \
     "data.csv" using (column("ln(t)")):(column("asin(ln(ln(p_m)))")) @blue_line

# asin(ln(ln(p))) vs asin(ln(t))

min_x = 0; max_x = 1.21

set output "plot/asin(ln(ln(p))) vs asin(ln(t)).png"
set xlabel "asin(ln(t))"
set ylabel "asin(ln(ln(p)))"
plot [min_x:max_x] "data.csv" using (column("asin(ln(t))")):(column("asin(ln(ln(p)))")) @black_line, \
     "data.csv" using (column("asin(ln(t))")):(column("asin(ln(ln(p_s)))")) @black_line, \
     "data.csv" using (column("asin(ln(t))")):(column("asin(ln(ln(p_r)))")) @black_line, \
     "data.csv" using (column("asin(ln(t))")):(column("asin(ln(ln(p_m)))")) @blue_line

# Δp vs asin(ln(t))

min_x = 0; max_x = 1.1
set terminal png size 1280,480

set output "plot/dp vs asin(ln(t)).png"
set xlabel "asin(ln(t))"
set ylabel "Δp"
plot [min_x:max_x] "data.csv" using (column("asin(ln(t))")):(column("dp")) @black_line, \
     "data.csv" using (column("asin(ln(t))")):(column("dp_m")) @blue_line

# ln(Δp) vs asin(ln(t))

set output "plot/ln(dp) vs asin(ln(t)).png"
set xlabel "asin(ln(t))"
set ylabel "ln(Δp)"
plot [min_x:max_x] "data.csv" using (column("asin(ln(t))")):(column("ln(dp)")) @black_line, \
     "data.csv" using (column("asin(ln(t))")):(column("ln(dp_m)")) @blue_line

# ln(ln(Δp)) vs asin(ln(t))

set output "plot/ln(ln(dp)) vs asin(ln(t)).png"
set xlabel "asin(ln(t))"
set ylabel "ln(ln(Δp))"
plot [min_x:max_x] "data.csv" using (column("asin(ln(t))")):(column("ln(ln(dp))")) @black_line, \
     "data.csv" using (column("asin(ln(t))")):(column("ln(ln(dp_m))")) @blue_line

# Δp, Δp_ixic vs asin(ln(t))

set format y "%.0f%%"
set yrange [-25:135]

set output "plot/dp, dp_ixic vs asin(ln(t)).png"
set xlabel "asin(ln(t))"
set ylabel "Δp"
plot [min_x:max_x] "data.csv" using (column("asin(ln(t))")):(100*column("dp%")) @gray_line title "Δp_{btc}", \
     "data.csv" using (column("asin(ln(t))")):(100*column("dp_ixic%")) @light_blue_line title "Δp_{ixic}", \
     "data.csv" using (column("asin(ln(t))")-0.005):(100*column("ema(dp%)")) @black_line title "ema(Δp_{btc})", \
     "data.csv" using (column("asin(ln(t))")-0.005):(100*column("ema(dp_ixic%)")) @thin_blue_line title "ema(Δp_{ixic})"

unset yrange

# Δp - Δp_ixic vs asin(ln(t))

set output "plot/dp - dp_ixic vs asin(ln(t)).png"
set xlabel "asin(ln(t))"
set ylabel "Δp"
plot [min_x:max_x] \
     "data.csv" using (column("asin(ln(t))")):(100*(column("dp%")-column("dp_ixic%"))) @gray_line title"Δp_{btc} - Δp_{ixic}", \
     "data.csv" using (column("asin(ln(t))")):(100*(column("dp_m%")-0.5)) @thin_blue_line title "Δp_{model}", \
     "data.csv" using (column("asin(ln(t))")-0.01):(100*column("ema(dp%-dp_ixic%)")) @black_line title "ema(Δp_{btc} - Δp_{ixic})", \
     0 @black_line notitle

# abs(Δp - Δp_ixic) vs asin(ln(t))

set fit quiet
set fit logfile "/dev/null"

f(x) = a*x+b
fit f(x) 'data.csv' using (column('asin(ln(t))')):(abs(column("dp%")-column("dp_ixic%"))) via a, b

set yrange [-20:100]

set output "plot/abs(dp - dp_ixic) vs asin(ln(t)).png"
set xlabel "asin(ln(t))"
set ylabel "abs(Δp)"
plot [min_x:max_x] \
     "data.csv" using (column("asin(ln(t))")):(100*abs(column("dp%")-column("dp_ixic%"))) @gray_line \
         title "abs(Δp_{btc} - Δp_{ixic})", \
     "data.csv" using (column("asin(ln(t))")-0.01):(100*abs(column("ema(dp%-dp_ixic%)"))) @black_line \
         title "abs(ema(Δp_{btc} - Δp_{ixic}))", \
     100*f(x) @blue_line, \
     0 @black_line
