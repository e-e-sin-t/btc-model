#!/usr/local/bin/gnuplot

w_png = 960; h_png = 480

set datafile separator "," 
set timefmt "%Y-%m-%d"
set decimal locale
set terminal png size w_png, h_png
set key noautotitle
set key spacing 1.75

basic_model = "data/basic-model.csv"
mirror_model = "data/mirror-model.csv"

line_color = "with lines linecolor "
black_line = line_color."'black'"
gray_line = line_color."'gray'"
blue_line = line_color."'blue'"
thick_blue_line = blue_line." linewidth 2"
light_blue_line = line_color."'light-blue'"

# p vs d

set xdata time
set format x "%Y"
set format y "$%'g"
set logscale y
set xlabel "date"
set ylabel "price"

set output "plot/p vs d.png"
plot ["2010-06-01":"2023-01-01"] [0.01:200000] \
     basic_model using (column("d")):(column("p")) @black_line, \
     basic_model using (column("d")):(column("p_s")) @black_line, \
     basic_model using (column("d")):(column("p_r")) @black_line, \
     basic_model using (column("d")):(column("p_m")) @thick_blue_line

# p_ixic vs d

set output "plot/ixic/p_ixic vs d.png"
plot ["2010-01-01":"2023-06-01"] [1500:25000] \
     basic_model using (column("d")):(column("p_ixic")) @black_line, \
     basic_model using (column("d")):(column("p_ixic_s")) @black_line, \
     basic_model using (column("d")):(column("p_ixic_r")) @black_line

# p vs d [basic]

set grid

set output "plot/future/p vs d [basic].png"
plot ["2010-01-01":"2040-01-01"] [0.01:200000] \
     basic_model using (column("d")):(column("p")) @black_line, \
     basic_model using (column("d")):(column("p_s")) @black_line, \
     basic_model using (column("d")):(column("p_r")) @black_line, \
     basic_model using (column("d")):(column("p_m")) @thick_blue_line

# p vs d [mirror]

set output "plot/future/p vs d [mirror].png"
plot ["2010-01-01":"2034-01-01"] [0.01:200000] \
     mirror_model using (column("d")):(column("p")) @black_line, \
     mirror_model using (column("d")):(column("p_s")) @black_line, \
     mirror_model using (column("d")):(column("p_r")) @black_line, \
     mirror_model using (column("d")):(column("p_m")) @thick_blue_line

# p vs d [mirror apy]

mid_d = '2021-06-01'
set label 10 '10%' at mid_d, 0.50
set label 30 '30%' at mid_d, 15
set label 50 '50%' at mid_d, 200
set label 75 '75%' at mid_d, 1750
set label 85 '85%' at mid_d, 9000

set output "plot/future/p vs d [mirror apy].png"
plot ["2010-01-01":"2034-01-01"] [0.01:200000] \
     mirror_model using (column("d")):(column("p")) @black_line, \
     mirror_model using (column("d")):(column("p_apy_10%")) @black_line, \
     mirror_model using (column("d")):(column("p_apy_30%")) @black_line, \
     mirror_model using (column("d")):(column("p_apy_50%")) @black_line, \
     mirror_model using (column("d")):(column("p_apy_70%")) @black_line, \
     mirror_model using (column("d")):(column("p_apy_85%")) @black_line, \
     mirror_model using (column("d")):(column("p_s")) @black_line, \
     mirror_model using (column("d")):(column("p_r")) @black_line, \
     mirror_model using (column("d")):(column("p_m")) @thick_blue_line

unset label 10; unset label 30; unset label 50; unset label 75; unset label 85

# p vs d [2020-2023]

set format x "%Y-%m"
unset logscale y
set grid

set output "plot/zoom/p vs d [2020-2025].png"
plot ["2020-06-01":"2025-06-01"] [5000:70000] \
     mirror_model using (column("d")):(column("p")) @black_line, \
     mirror_model using (column("d")):(column("p_s")) @black_line, \
     mirror_model using (column("d")):(column("p_r")) @black_line, \
     mirror_model using (column("d")):(column("p_m")) @blue_line

# p vs d [2010]

set format x "%Y-%m-%d"
set logscale y

set output "plot/zoom/p vs d [2010].png"
plot ["2010-10-01":"2011-01-01"] [0.01:2] \
     basic_model using (column("d")):(column("p")) @black_line, \
     basic_model using (column("d")):(column("p_s")) @black_line, \
     basic_model using (column("d")):(column("p_r")) @black_line, \
     basic_model using (column("d")):(column("p_m")) @blue_line

unset logscale y; unset xdata; unset format; unset grid

# ln(p) vs t

set output "plot/scale/1. ln(p) vs t.png"
set xlabel "t"
set ylabel "ln(p)"
plot [0:12] \
     basic_model using (column("t")):(column("ln(p)")) @black_line, \
     basic_model using (column("t")):(column("ln(p_s)")) @black_line, \
     basic_model using (column("t")):(column("ln(p_r)")) @black_line, \
     basic_model using (column("t")):(column("ln(p_m)")) @thick_blue_line

# ln(p) vs ln(t)

set xrange [0:8.5]

set output "plot/scale/2. ln(p) vs ln(t).png"
set xlabel "ln(t)"
set ylabel "ln(p)"
plot basic_model using (column("ln(t)")):(column("ln(p)")) @black_line, \
     basic_model using (column("ln(t)")):(column("ln(p_s)")) @black_line, \
     basic_model using (column("ln(t)")):(column("ln(p_r)")) @black_line, \
     basic_model using (column("ln(t)")):(column("ln(p_m)")) @thick_blue_line

# ln(ln(p)) vs ln(t)

set output "plot/scale/3. ln(ln(p)) vs ln(t).png"
set xlabel "ln(t)"
set ylabel "ln(ln(p))"
plot basic_model using (column("ln(t)")):(column("ln(ln(p))")) @black_line, \
     basic_model using (column("ln(t)")):(column("ln(ln(p_s))")) @black_line, \
     basic_model using (column("ln(t)")):(column("ln(ln(p_r))")) @black_line, \
     basic_model using (column("ln(t)")):(column("ln(ln(p_m))")) @thick_blue_line

# asin(ln(ln(p))) vs ln(t)

set output "plot/scale/4. asin(ln(ln(p))) vs ln(t).png"
set xlabel "ln(t)"
set ylabel "asin(ln(ln(p)))"
plot basic_model using (column("ln(t)")):(column("asin(ln(ln(p)))")) @black_line, \
     basic_model using (column("ln(t)")):(column("asin(ln(ln(p_s)))")) @black_line, \
     basic_model using (column("ln(t)")):(column("asin(ln(ln(p_r)))")) @black_line, \
     basic_model using (column("ln(t)")):(column("asin(ln(ln(p_m)))")) @thick_blue_line

unset xrange

# asin(ln(ln(p))) vs asin(ln(t))

set output "plot/scale/5. asin(ln(ln(p))) vs asin(ln(t)).png"
set xlabel "asin(ln(t))"
set ylabel "asin(ln(ln(p)))"
plot [0:1.1] \
     basic_model using (column("asin(ln(t))")):(column("asin(ln(ln(p)))")) @black_line, \
     basic_model using (column("asin(ln(t))")):(column("asin(ln(ln(p_s)))")) @black_line, \
     basic_model using (column("asin(ln(t))")):(column("asin(ln(ln(p_r)))")) @black_line, \
     basic_model using (column("asin(ln(t))")):(column("asin(ln(ln(p_m)))")) @thick_blue_line

# asin(ln(ln(p))) vs asin(ln(t)) [preview]

set terminal png size 3*w_png/4, 3*h_png/4

set output "plot/scale/5. asin(ln(ln(p))) vs asin(ln(t)) [preview].png"
plot [0:1.1] \
     basic_model using (column("asin(ln(t))")):(column("asin(ln(ln(p)))")) @black_line, \
     basic_model using (column("asin(ln(t))")):(column("asin(ln(ln(p_s)))")) @black_line, \
     basic_model using (column("asin(ln(t))")):(column("asin(ln(ln(p_r)))")) @black_line, \
     basic_model using (column("asin(ln(t))")):(column("asin(ln(ln(p_m)))")) @thick_blue_line
set terminal png size w_png, h_png

# Δp vs asin(ln(t))

set terminal png size 1280,480

set output "plot/scale/6a. dp vs asin(ln(t)).png"
set xlabel "asin(ln(t))"
set xrange [0:1.1]
set ylabel "Δp"
plot basic_model using (column("asin(ln(t))")):(column("dp")) @black_line, \
     basic_model using (column("asin(ln(t))")):(column("dp_m")) @thick_blue_line

# ln(Δp) vs asin(ln(t))

set output "plot/scale/6b. ln(dp) vs asin(ln(t)).png"
set xlabel "asin(ln(t))"
set ylabel "ln(Δp)"
plot basic_model using (column("asin(ln(t))")):(column("ln(dp)")) @black_line, \
     basic_model using (column("asin(ln(t))")):(column("ln(dp_m)")) @thick_blue_line

# ln(ln(Δp)) vs asin(ln(t))

set output "plot/scale/6c. ln(ln(dp)) vs asin(ln(t)).png"
set xlabel "asin(ln(t))"
set ylabel "ln(ln(Δp))"
plot basic_model using (column("asin(ln(t))")):(column("ln(ln(dp))")) @black_line, \
     basic_model using (column("asin(ln(t))")):(column("ln(ln(dp_m))")) @thick_blue_line

# Δp, Δp_ixic vs asin(ln(t))

set format y "%.0f%%"

set output "plot/ixic/dp, dp_ixic vs asin(ln(t)).png"
set xlabel "asin(ln(t))"
set ylabel "Δp"
set yrange [-25:135]
plot basic_model using (column("asin(ln(t))")):(100*column("dp%")) @gray_line title "ln(ln(Δp_{btc}))", \
     basic_model using (column("asin(ln(t))")):(100*column("dp_ixic%")) @light_blue_line title "ln(Δp_{ixic})", \
     basic_model using (column("asin(ln(t))")-0.005):(100*column("ema(dp%)")) @black_line title "ema(ln(ln(Δp_{btc})))", \
     basic_model using (column("asin(ln(t))")-0.005):(100*column("ema(dp_ixic%)")) @blue_line title "ema(ln(Δp_{ixic}))"

unset yrange

# Δp - Δp_ixic vs asin(ln(t))

set output "plot/ixic/dp - dp_ixic vs asin(ln(t)).png"
set xlabel "asin(ln(t))"
set ylabel "Δp"
plot basic_model using (column("asin(ln(t))")):(100*(column("dp%")-column("dp_ixic%"))) @gray_line \
         title"ln(ln(Δp_{btc})) - ln(Δp_{ixic})", \
     basic_model using (column("asin(ln(t))")):(100*(column("dp_m%")-0.5)) @blue_line title "ln(ln(Δp_{model}))", \
     basic_model using (column("asin(ln(t))")-0.01):(100*column("ema(dp%-dp_ixic%)")) @black_line \
         title "ema(ln(ln(Δp_{btc})) - ln(Δp_{ixic}))", \
     0 @black_line notitle

# abs(Δp - Δp_ixic) vs asin(ln(t))

set fit quiet
set fit logfile "/dev/null"

f(x) = a*exp(-b*x)
fit f(x) basic_model using (column('asin(ln(t))')):(abs(column("dp%")-column("dp_ixic%"))) via a, b

set output "plot/ixic/abs(dp - dp_ixic) vs asin(ln(t)).png"
set xlabel "asin(ln(t))"
set ylabel "abs(Δp)"
set yrange [-20:100] 
plot basic_model using (column("asin(ln(t))")):(100*abs(column("dp%")-column("dp_ixic%"))) @gray_line \
         title "abs(ln(ln(Δp_{btc})) - ln(Δp_{ixic}))", \
     basic_model using (column("asin(ln(t))")-0.01):(100*abs(column("ema(dp%-dp_ixic%)"))) @black_line \
         title "abs(ema(ln(ln(Δp_{btc})) - ln(Δp_{ixic})))", \
     100*f(x) @blue_line title sprintf("fit(a·e^{-b·t}), a=%0.3f, b=%0.3f", a, b), \
     0 @black_line

unset xrange; unset yrange; unset format y
